managed;
strict (2);
with draft;



define behavior for ZDD_TRAVEL_ANA alias travel
implementation in class zbp_dd_travel_ana unique
persistent table zana_travel_1234
draft table zdan_travel_1234
lock master total etag Lastchangedat
authorization master ( instance )
etag master LocalLastChangedAt
{
  create;
  update;
  delete;
  field ( numbering : managed, readonly ) TravelUuid;
  field ( readonly ) Travelid, TotalPrice, TravelStatus;
  field ( readonly ) CreatedAt, Createdby, LocalLastChangedAt, LastChangedAt, LastChangedBy;
  field ( mandatory ) CustomerId, AgencyId;
  association _Booking { create; with draft; }

  action ( features : instance ) accepTravel result [1] $self;
  action ( features : instance ) rejectTravel result [1] $self;
  internal action recalcTotalPrice;

  determination setInitialStatus on modify { create; }
  determination calculateTotalPrice on modify { field BookingFee, CurrencyCode; }
  determination calculateTravelID on save { create; }
  validation validateAgency on save { field AgencyID; create; }
  validation validateCustomer on save { field CustomerID; create; }
  validation validateDates on save { field BeginDate, EndDate; create; }

  draft action Edit;
  draft action Activate optimized;
  draft action Discard;
  draft action Resume;
   draft determine action Prepare  {
    validation validateAgency;
    validation validateCustomer;
    validation validateDates;
  }
  mapping for zana_travel_1234
    {
      TravelUUID         = travel_uuid;
      TravelID           = travel_id;
      AgencyID           = agency_id;
      CustomerID         = customer_id;
      BeginDate          = begin_date;
      EndDate            = end_date;
      BookingFee         = booking_fee;
      TotalPrice         = total_price;
      CurrencyCode       = currency_code;
      Description        = description;
      TravelStatus       = overall_status;
      CreatedBy          = created_by;
      CreatedAt          = created_at;
      LastChangedBy      = last_changed_by;
      LastChangedAt      = last_changed_at;
      LocalLastChangedAt = local_last_changed_at;
    }

}

define behavior for ZDD_BOOKING_ANA alias booking
implementation in class zbp_dd_booking_ana unique
persistent table zana_book_1234
draft table zdana_book_1234
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{
  update;
  delete;
  association _Travel { with draft; }
  field ( numbering : managed, readonly ) BookingUuid;
  field ( readonly ) TravelUuid, bookingid;
  field ( readonly ) LocalLastChangedAt, CreatedBy, LastChangedBy;

  determination CalculateBookingId on modify { create; }
  determination CalculateTotalPrice on modify { field flightPrice, CurrencyCode; }
  mapping for zana_book_1234
    {
      BookingUUID        = booking_uuid;
      TravelUUID         = travel_uuid;
      BookingID          = booking_id;
      BookingDate        = booking_date;
      CustomerID         = customer_id;
      CarrierID          = carrier_id;
      ConnectionID       = connection_id;
      FlightDate         = flight_date;
      FlightPrice        = flight_price;
      CurrencyCode       = currency_code;
      CreatedBy          = created_by;
      LastChangedBy      = last_changed_by;
      LocalLastChangedAt = local_last_changed_at;
    }
}